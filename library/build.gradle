apply plugin: 'java-library'
apply plugin: 'maven-publish'

apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'io.freefair.javadoc-links'

apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compileOnly deps.lombok
    compileOnly deps.jsr305

    api deps.retrofit
    api deps.retrofitMoshi
    api deps.okHttp
    api deps.moshi

    testCompileOnly deps.lombok
    testCompileOnly deps.jsr305

    testImplementation deps.junit
    testImplementation deps.assertj
    testImplementation deps.mockwebserver
    testImplementation deps.privateConstructor
}

task delombok(type: JavaExec, dependsOn: compileJava) {
    String buildConfigInputDir = "$buildDir/gen/buildconfig/src/main"

    ext.outputDir = 'build/delombok'
    outputs.dir(outputDir)
    inputs.dir(buildConfigInputDir)

    args += "delombok"

    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)

        args += it
    }

    args += buildConfigInputDir
    args += "--target=$outputDir"
    args += "--classpath=$configurations.compileClasspath.asPath"

    classpath configurations.compileOnly.asPath
    main = "lombok.launch.Main"
}

buildConfig {
    packageName = 'me.proxer.library'
}

javadoc {
    dependsOn delombok
    failOnError false

    source = delombok.outputDir

    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.addBooleanOption('Xdoclint:-missing', true)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

task sourceJar(type: Jar, dependsOn: delombok) {
    from delombok.outputDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
