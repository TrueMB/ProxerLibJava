plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.dokka"

    id "java-library"
    id "maven-publish"

    id "jacoco"
    id "io.gitlab.arturbosch.detekt"
    id "org.jlleitschuh.gradle.ktlint"

    id "com.github.ben-manes.versions"
    id "de.fuerstenau.buildconfig"

    id "idea"
}

apply from: "${rootDir}/gradle/dependencies.gradle"

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "html"
    outputDirectory = javadoc.destinationDir
    enabled = JavaVersion.current().isJava8()

    includeNonPublic = false
    jdkVersion = 8

    externalDocumentationLink { url = new URL("https://square.github.io/retrofit/2.x/retrofit/") }
    externalDocumentationLink { url = new URL("https://square.github.io/okhttp/3.x/okhttp/") }
    externalDocumentationLink { url = new URL("https://square.github.io/moshi/1.x/moshi/") }

    packageOptions {
        prefix = "com.github.proxer.library.enums"
        reportUndocumented = false
    }
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = "javadoc"

    from javadoc.destinationDir
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
        freeCompilerArgs = ["-Xjsr305=strict", "-progressive"]
    }
}

kapt {
    useBuildCache = true
}

test {
    finalizedBy jacocoTestReport

    useJUnitPlatform()
}

buildConfig {
    packageName = "me.proxer.library"
    version = project.version
}

jacoco {
    setToolVersion(jacocoVersion)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

ktlint {
    disabledRules = ["import-ordering"]
    reporters {
        reporter "checkstyle"
    }
}

detekt {
    config = files("$rootDir/config/detekt.yml")
    failFast = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "ProxerLibJava"

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = "ProxerLibJava"
                description = "A JVM and Android library, implementing the API of the Proxer.Me website"
                url = "https://github.com/proxer/ProxerLibJava"

                scm {
                    url = "https://github.com/proxer/ProxerLibJava"
                    connection = "scm:git:https://github.com/proxer/ProxerLibJava"
                    developerConnection = "scm:git:git@github.com/proxer/ProxerLibJava"
                    tag = "HEAD"
                }

                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/proxer/ProxerLibJava/issues"
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
            }
        }
    }
}
