apply plugin: 'idea'

apply plugin: 'java-library'
apply plugin: 'maven-publish'

apply plugin: "jacoco"
apply plugin: "io.freefair.javadoc-links"

tasks.matching { it instanceof JavaCompile && it.name != "compileTestJava" }.all {
    sourceCompatibility = rootProject.ext.sourceCompatibility
    targetCompatibility = rootProject.ext.targetCompatibility

    if (System.getenv('CIRCLECI')) {
        options.bootClasspath = "/usr/lib/jvm/java-7-openjdk-amd64/jre/lib/rt.jar"
    }
}

dependencies {
    api deps.jetbrainsAnnotations

    api deps.retrofit
    api deps.retrofitMoshi
    api deps.okhttp
    api deps.moshi

    compileOnly deps.lombok

    testImplementation deps.junit
    testImplementation deps.assertj

    testImplementation deps.mockwebserver
    testImplementation deps.privateConstructor

    testCompileOnly deps.lombok
}

task delombok {
    dependsOn compileJava

    def srcPath = 'src/main/java'
    def delombokPath = 'build/delombok'

    inputs.files file(srcPath)
    outputs.dir file(delombokPath)

    ext.outputDir = delombokPath

    doLast {
        ant.taskdef(name: 'delombok', classname: 'lombok.delombok.ant.Tasks$Delombok',
                classpath: configurations.compileOnly.asPath)
        ant.delombok(encoding: 'UTF-8', from: srcPath, to: delombokPath,
                classpath: configurations.compileClasspath.asPath)
    }
}

javadoc {
    dependsOn delombok

    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.addBooleanOption('Xdoclint:-missing', true)

    source = delombok.outputDir
    failOnError = false
}

task sourceJar(type: Jar, dependsOn: classes) {
    from delombok.outputDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
